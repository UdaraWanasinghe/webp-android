apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // standard metadata
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                // publication component defined by the android library plugin
                from components.release

                // pom file
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'libwebp JNI bindings for Android.'
                    url = 'https://github.com/UdaraWanasinghe/webp-android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/UdaraWanasinghe/webp-android/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'UdaraWanasinghe'
                            name = 'Udara Wanasinghe'
                            email = 'udara.developer@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/UdaraWanasinghe/webp-android.git'
                        developerConnection = 'scm:git:ssh://github.com/UdaraWanasinghe/webp-android.git'
                        url = 'https://github.com/UdaraWanasinghe/webp-android'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}